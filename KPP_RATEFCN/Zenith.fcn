
   ! zenith angle time dependent function: Zenith (see supplement section 3)
    
  REAL(kind=dp) FUNCTION Zenith(Time)
    !-----------------------------------------------------------------------!
    ! Input:
    !   - Time
    REAL(dp) :: Time
    !-----------------------------------------------------------------------!
    ! Output:
    !   - sun angle Zenith
    !-----------------------------------------------------------------------!
    ! Temporary variables:
    !INTEGER :: IDAT
    REAL(dp) :: LBGMT, LZGMT
    REAL(dp) :: ML
    ! 
    REAL(dp) :: GMT
    REAL(dp) :: RLT, RPHI
    !    
    INTEGER  :: IIYEAR, IYEAR, IMTH, IDAY, IIY, NYEARS, LEAP, NOLEAP
    REAL(dp) :: YREF,YR
    !   
    INTEGER  :: I, IJ, JD, IJD, IN
    REAL(dp) :: D, RML, W, WR, EC, EPSI, PEPSI, YT, CW, SW, SSW  & 
    &         , EYT, FEQT1, FEQT2, FEQT3, FEQT4, FEQT5, FEQT6 &
    &         , FEQT7, FEQT, EQT
    !         
    REAL(dp) :: REQT, RA, RRA, TAB, RDECL, DECL, ZPT, CSZ, ZR    &
    &         , CAZ, RAZ, AZIMUTH

    REAL(dp), PARAMETER :: Pi   = 4.0_dp*ATAN(1.0_dp)
    REAL(dp), PARAMETER :: rlat = 45.0_dp
    REAL(dp), PARAMETER :: rlon = 0.0_dp
    INTEGER,  PARAMETER :: idate= 010621
    REAL(dp), PARAMETER :: DR   = Pi / 180.d0
    !           
    INTEGER :: IMN(12)
    DATA IMN/31,28,31,30,31,30,31,31,30,31,30,31/
    !
    !----------------------------------------------------------------------!
    !
    ! set GMT
    GMT = Time / 3600.0_dp
    !
    !  convert to radians
    RLT = rlat*DR
    RPHI = rlon*DR
    !
    !  parse date
    IIYEAR = iDate/10000
    IYEAR = 19*100 + IIYEAR
    IF (IIYEAR <= 50) IYEAR = IYEAR + 100 
    IMTH = (iDate - IIYEAR*10000)/100
    IDAY = iDate - IIYEAR*10000 - IMTH*100
    !
    !  identify and correct leap years
    IIY = (IIYEAR/4)*4
    IF(IIY.EQ.IIYEAR) IMN(2) = 29
    !
    !  count days from Dec.31,1973 to Jan 1, YEAR, then add to 2,442,047.5
    YREF =  2442047.5_dp
    NYEARS = IYEAR - 1974
    LEAP = (NYEARS+1)/4
    IF(NYEARS.LE.-1) LEAP = (NYEARS-2)/4
    NOLEAP = NYEARS - LEAP
    YR = YREF + 365.0_dp*NOLEAP + 366.0_dp*LEAP
    !
    IJD = 0
    IN = IMTH - 1
    IF(IN.EQ.0) GO TO 40
    DO 30 I=1,IN
    IJD = IJD + IMN(I)
    30   CONTINUE
    IJD = IJD + IDAY
    GO TO 50
    40   IJD = IDAY
    50   IJ = IYEAR - 1973
    !
    !      print julian days current "ijd"
    JD = IJD + (YR - YREF)
    D = JD + GMT/24.0_dp
    !
    !      calc geom mean longitude
    ML = 279.2801988_dp + .9856473354_dp*D + 2.267e-13_dp*D*D
    RML = ML*DR
    !
    !      calc equation of time in sec
    !      w = mean long of perigee
    !      e = eccentricity
    !      epsi = mean obliquity of ecliptic
    W = 282.4932328_dp + 4.70684e-5_dp*D + 3.39e-13_dp*D*D
    WR = W*DR
    EC = 1.6720041e-2_dp - 1.1444e-9_dp*D - 9.4e-17_dp*D*D
    EPSI = 23.44266511_dp - 3.5626e-7_dp*D - 1.23e-15_dp*D*D
    PEPSI = EPSI*DR
    YT = (TAN(PEPSI/2.0_dp))**2
    CW = COS(WR)
    SW = SIN(WR)
    SSW = SIN(2.0_dp*WR)
    EYT = 2.0_dp*EC*YT
    FEQT1 = SIN(RML)*(-EYT*CW - 2.0_dp*EC*CW)
    FEQT2 = COS(RML)*(2.0_dp*EC*SW - EYT*SW)
    FEQT3 = SIN(2.0_dp*RML)*(YT - (5.0_dp*EC*EC/4.0_dp)*(CW*CW-SW*SW))
    FEQT4 = COS(2.0_dp*RML)*(5.0_dp*EC**2*SSW/4.0_dp)
    FEQT5 = SIN(3.0_dp*RML)*(EYT*CW)
    FEQT6 = COS(3.0_dp*RML)*(-EYT*SW)
    FEQT7 = -SIN(4.0_dp*RML)*(YT*YT/2.0_dp)
    FEQT = FEQT1 + FEQT2 + FEQT3 + FEQT4 + FEQT5 + FEQT6 + FEQT7
    EQT = FEQT*13751.0_dp
    !
    !   convert eq of time from sec to deg
    REQT = EQT/240.0_dp
    !
    !   calc right ascension in rads
    RA = ML - REQT
    RRA = RA*DR
    !
    !   calc declination in rads, deg
    TAB = 0.43360_dp*SIN(RRA)
    RDECL = ATAN(TAB)
    DECL = RDECL/DR
    !
    !   calc local 3600.0_dp angle
    LBGMT = 12.0_dp - EQT/3600.0_dp + rlon*24.0_dp/360.0_dp
    LZGMT = 15.0_dp*(GMT - LBGMT)
    ZPT = LZGMT*DR
    CSZ = SIN(RLT)*SIN(RDECL) + COS(RLT)*COS(RDECL)*COS(ZPT)
    ZR = ACOS(CSZ)
    ! 
    !   calc local solar azimuth
    CAZ = (SIN(RDECL) - SIN(RLT)*COS(ZR))/(COS(RLT)*SIN(ZR))
    RAZ = ACOS(CAZ)
    AZIMUTH = RAZ/DR
    !
    !--- set Zenith Angle
    Zenith =  1.745329252e-02_dp * ZR/DR
  END FUNCTION Zenith